configfile: "config/config.yml"

include: "rules/common.smk"
include: "rules/aggregate.smk"
include: "rules/clone.smk"
include: "rules/permutations.smk"
include: "rules/logs.smk"
include: "rules/solve.smk"

report: "report/workflow.rst"

rule all:
    localrule: True
    input:
        "results/total_times.json",
        expand("results/plots/fipy~{rev}/{benchmark}/{plot}.png",
               rev=config["fipy_revs"],
               benchmark=config["benchmarks"],
               plot=["total", "prepare", "solve"])

rule bootstrap:
    input:
        expand("results/fipy~{rev}/suite~{suite}/environment.yml",
               rev=config["fipy_revs"],
               suite=config["suites"])

rule aggregate_all_results:
    output:
        "results/all.json"
    input:
        logs=expand("results/fipy~{rev}/suite~{suite}/results.json",
                    rev=config["fipy_revs"],
                    suite=config["suites"])
    log:
        "logs/aggregate_all_results.log"
    run:
        concat_json(input.logs, output[0], log[0])

rule aggregate_results:
    output:
        "results/fipy~{rev}/suite~{suite}/results.json"
    input:
        permutations="config/all_permutations.csv",
        logs=expand("results/fipy~{{rev}}/suite~{{suite}}/{id}/solver.json",
                    id=get_permutation_ids)
    log:
        "logs/fipy~{rev}/suite~{suite}/aggregate_results.log"
    run:
        concat_json(input.logs, output[0], log[0])
