configfile: "config/config.yml"

include: "rules/common.smk"
include: "rules/aggregate.smk"
include: "rules/clone.smk"
include: "rules/permutations.smk"
include: "rules/logs.smk"
include: "rules/solve.smk"

report: "report/workflow.rst"

rule all:
    input:
        "results/total_times.json",
        expand("results/plots/fipy~{rev}/{benchmark}/{plot}.png",
               rev=config["fipy_revs"],
               benchmark=config["benchmarks"],
               plot=["total", "prepare", "solve"])

rule aggregate_results:
    output:
        "results/all.json"
    input:
        "config/all_permutations.csv",
        logs=expand("results/{id}/solver.json",
                    id=get_all_permutation_ids)
    log:
        "logs/aggregate_results.log"
    run:
        concat_json(input.logs, output[0], log[0])

# this rule addressses a bootstrapping problem
# list_solvers and list_preconditioners require
# conda environments to run, but the conda environments
# may not be created, yet. Checkpoint rules produce
# messed up graphs.

rule all_envs:
    input:
        expand("workflow/envs/fipy~{rev}/benchmark_{suite}.yml",
               rev=config["fipy_revs"],
               suite=config["suites"])
