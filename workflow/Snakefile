configfile: "config/config.yml"

include: "rules/common.smk"

rule all:
    localrule: True
    input:
        logs=expand("results/{id}/solver.log",
                    id=get_all_permutation_ids)

rule solve:
    output:
        "results/{id}/solver.log"
    input:
        "config/all_permutations.csv"
    conda:
        get_conda_environment_from_id
    log:
        notebook="logs/notebooks/benchmark_{id}.ipynb"
    notebook:
        "notebooks/binary_phase_field.py.ipynb"

checkpoint aggregate_permutations:
    output:
        "config/all_permutations.csv"
    input:
        "workflow/envs/fipy_benchmark_petsc.yml"
    conda:
        "envs/snakemake.yml"
    log:
        "logs/fipy_petsc_permutations.log"
    script:
        "scripts/rev_and_suite_permutations.py"

checkpoint render_conda_template:
    localrule: True
    output:
        "workflow/envs/fipy_benchmark_petsc.yml",
    input:
        template="workflow/envs/benchmark_petsc.yml",
    log:
        "logs/render_conda_template_petsc.log"
    template_engine:
        "yte"
