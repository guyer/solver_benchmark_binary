import numpy as np
import pandas as pd

configfile: "config/config.yml"

# calculate dimensions that produce steps in orders of magnitude in number of cells
SIZES = (10**(np.arange(np.log10(config["size"]["min"]),
                        np.log10(config["size"]["max"])+1,
                        1.)/2)).round().astype(int)**2

include: "rules/common.smk"
include: "rules/aggregate.smk"
include: "rules/permutations.smk"
include: "rules/logs.smk"
include: "rules/solve.smk"
include: "rules/plot_scaling.smk"

report: "report/workflow.rst"

permutations = pd.read_csv("config/permutations.csv", index_col="uuid")

rule all:
    input:
        "results/all.json",
        "results/total_times.json",
        get_all_plots

rule aggregate_results:
    output:
        "results/all.json"
    input:
        logs=expand("results/{id}/solver.json",
                    id=permutations.index)
    log:
        "results/all.log"
    run:
        concat_json(input.logs, output[0], log[0])

